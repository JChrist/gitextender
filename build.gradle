plugins {
    id "java"
    id "org.jetbrains.intellij.platform" version "2.10.3"
    id "jacoco"
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.jetbrains.team/maven/p/ij/intellij-dependencies")
    }
    intellijPlatform {
        defaultRepositories()
    }
}

group 'gr.jchrist'
version '0.8.2'

sourceCompatibility = '17'
targetCompatibility = '17'

dependencies {
    intellijPlatform {
        intellijIdeaCommunity('2024.2')
        //intellijIdeaCommunity('LATEST-EAP-SNAPSHOT')
        bundledPlugin 'Git4Idea'

        instrumentationTools()

        testFramework(org.jetbrains.intellij.platform.gradle.TestFrameworkType.Platform.INSTANCE)
    }

    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.opentest4j:opentest4j:1.3.0'
}

intellijPlatform {
    buildSearchableOptions = false

    pluginConfiguration {
        id = 'gr.jchrist.gitextender'
        name = 'Git Extender'
        version = project.version
        // patchPluginXml.enabled = false
        ideaVersion {
            sinceBuild = '241'
            untilBuild = provider { null }
        }
    }
}

jacocoTestReport {
    classDirectories.setFrom(instrumentCode)
    reports {
        xml.required = true
        html.required = false
        csv.required = false
    }
}

//specify here that jacoco test report should execute after test
test {
    finalizedBy jacocoTestReport
    jacoco {
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }
}
